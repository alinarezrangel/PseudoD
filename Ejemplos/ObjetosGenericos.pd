[ ***************************************************
*****************
** ObjetosGenericos.pd: Utiliza DuckTyping.
*****************
*****************************************************
Copyright 2016 Alejandro Linarez Rangel

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

 http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*************************************************** ]

[ Importamos la biblioteca de entrada y salida de PseudoD ]
utilizar entsal.pseudo

[ ****************************************************************
En el duck typing, demostramos como ignorar el tipo de un objeto
y simplemente utilizar las características que nos interesan de
dicho objeto.
**************************************************************** ]

[ La clase abstracta IQuackable representa todo lo que pueda hacer "quack" ]
clase abstracta IQuackable hereda Objeto
	metodo quack
finclase

[ Aquí tenemos dos objetos que pueden hacer quack: una persona y un pato ]
clase Pato hereda Objeto extiende IQuackable
	metodo quack
finclase

[ Implementamos el método quack: ]
funcion Pato#quack con yo
	llamar EscribirLinea {Quack!Quack!Quack!} finargs
finfun

[ Y una persona también puede hacer quack: ]
clase Persona hereda Objeto extiende IQuackable
	metodo quack
finclase

[ Implementamos el método quack: ]
funcion Persona#quack con yo
	llamar EscribirLinea
		{Soy una persona imitando un pato: quack quack quack}
	finargs
finfun

[ Tenemos una funcion que no verifica el tipo, solo utiliza el método quack ]
funcion Quackear con objeto
	llamar <objeto>#quack finargs
finfun

[ Lo probamos: ]
instancia Pato pato
instancia Persona persona

llamar Quackear pato#NOMBRE finargs
llamar Quackear persona#NOMBRE finargs
